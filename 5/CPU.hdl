// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	//// Replace this comment with your code.
    //lets use the implementation described in the video unit
    //decoding the instruction
    Or(a=instruction[15] , b=false , out=instC );
    Not(in=instC , out=instA );
    //two types of instructs are directed to register A:
    //A instruct or C instruct with the last d bit is on
    And(a=instC , b=instruction[5] , out=instCdestA );
    Or(a=instA , b=instCdestA , out=savetoA );
    //choose which adress to load
    Mux16(a=instruction , b=aluout , sel=instCdestA , out=Amux );
    ARegister(in=Amux , load=savetoA , out= address,out[0..14]=addressM);
    //second mux we decide based on 'a' bit in instruction
    //if the input the alu will get is from register A or inM
    Mux16(a= address , b=inM , sel=instruction[12] , out=MorAin );
    //Dregister
    //load data if c instruction and bit 4 is on
    And(a=instruction[4] , b=instC , out=loadD );
    DRegister(in=aluout , load=loadD , out=Din );
    //instruction bits between 6 to 11 are the control bits for the alu
    ALU(x=Din, y= MorAin  , 
    zx=instruction[11] , nx=instruction[10] , 
    zy=instruction[9] , ny=instruction[8] , 
    f=instruction[7] , no=instruction[6] , 
    out=aluout , zr=aluzero , 
    ng=negative );
    Or16(a=aluout , b=false , out=outM );
    And(a=instruction[3] , b=instC , out=writeM ); 
    //now we need to determine the signals for load and inc for pc


    //pc
    //reset is easy and so does input
    Not(in=aluzero , out= notzero);
    Not(in= negative, out= nonnegative);
    And(a=nonnegative , b= notzero, out=positive );


    //increase should be no jump so we need to check the j bits
    //in should be the not of load which is bassicly jump
    //lets check all the conditions to jump
    //pc set to A if all j bits are up
    //else we should decide
    //if first bit is on and we got positive answer from alu so its jgt
    And(a=positive , b=instruction[0] , out=JGT );
    //JLT
    And(a=negative , b=instruction[2] , out=JLT );
    //JEQ
    And(a=aluzero , b=instruction[1] , out=JEQ );
    //checking if one condition is met
    Or(a=JGT , b=JLT , out=jump1 );
    Or(a=jump1 , b=JEQ , out=jump );
    And(a=instC , b=jump , out=loadjump );
    Not(in=loadjump , out=plus );
    PC(in=address , load=loadjump  , inc=plus , 
    reset=reset , out[0..14]=pc );


    






}