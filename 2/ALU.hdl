// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //x handling
    Mux16(a=x , b=false , sel=zx , out=xorz );
    Not16(in=xorz , out=nxorz );
    Mux16(a=xorz , b=nxorz , sel=nx , out=outx );
    //y handling
    Mux16(a=y , b=false , sel=zy , out=yorz );
    Not16(in=yorz , out=nyorz );
    Mux16(a=yorz , b=nyorz , sel=ny , out=outy );

    //f handling
    Add16(a =outx , b =outy , out =xaddy );
    And16(a=outx , b=outy , out=xandy );
    Mux16(a=xandy , b=xaddy , sel=f , out=outf );

    //no handling
    Not16(in=outf , out=noutf );
    Mux16(a=outf , b=noutf , sel=no , out[0..7]=out7,out[8..14]=out14,out[15]=out15 );
    Mux16(a[0..7]=out7,a[8..14]=out14,a[15]=out15 , b=true , sel=false , out=out );

    And(a=out15 , b=true , out=ng );
    Or8Way(in=out7 , out=or1 );
    Or8Way(in[0..6]=out14,in[7]=out15 , out=or2 );
    Or(a=or1 , b=or2 , out=nzr );
    Not(in=nzr , out=zr );











}